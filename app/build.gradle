apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId 'fr.openium.auvergnewebcams'
        minSdkVersion 21
        targetSdkVersion 29

        versionCode 34
        versionName '1.4.4'

        renderscriptTargetApi 21
        renderscriptSupportModeEnabled = true

        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
        }
    }

    signingConfigs {
        debug {
            storeFile file("keys/debug.keystore")
        }

        release {
            storeFile file('keys/release.keystore')
            storePassword 'AWCOpenium'
            keyAlias 'AuvergneWebCamsOpenium'
            keyPassword 'AWCOpenium'
        }
    }

    buildTypes {
        debug {
            // Optimization
            minifyEnabled false
            ext.alwaysUpdateBuildId = false

            // Allow debug and release on the same device
            versionNameSuffix '-debug'
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    //Export room schema for easy migration
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    // Needed for tests purposes and for exoPlayer/coil
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    // Apk size optimization
    packagingOptions {
        exclude "okhttp3/**"
        exclude "com/google/api/**"
        exclude "kotlin/**"
        exclude "META-INF/*.version"
        exclude "META-INF/**.pro"
        exclude "META-INF/services/**"
        exclude "**/*.kotlin_module"
        exclude "**/*.properties"
        exclude "**/*.xml"
    }
}

repositories {
    mavenCentral()
    google()
    jcenter()
    maven { url 'https://maven.openium.fr' }
    maven { url 'https://jitpack.io' }

    flatDir {
        dirs 'libs'
    }
}

kapt { useBuildCache = true }

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // --- Kotlin ---
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // --- AndroidX ---
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta7'
    implementation 'com.google.android.material:material:1.2.0-beta01'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'androidx.work:work-runtime-ktx:2.3.4'
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // --- Firebase ---
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.1'
    implementation 'com.google.firebase:firebase-analytics:17.4.3'

    // --- Openium Tools ---
    implementation 'fr.openium:kotlin-tools:1.0.14'
    implementation 'fr.openium:rx-tools:1.0.5'

    // --- KTX ---
    implementation 'androidx.core:core-ktx:1.3.0'

    // --- Koin
    implementation "org.koin:koin-android:2.1.4"
    implementation "org.koin:koin-androidx-viewmodel:2.1.4"

    // --- Rx ---
    implementation 'io.reactivex.rxjava2:rxjava:2.2.15'
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.1.0'
    implementation 'com.jakewharton.rxbinding3:rxbinding-appcompat:3.1.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    // --- Retrofit ---
    implementation 'com.squareup.retrofit2:retrofit:2.7.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.4.0'
    debugImplementation 'com.squareup.retrofit2:retrofit-mock:2.6.2'

    // --- Log ---
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // --- Test ---
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:core-ktx:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestUtil 'androidx.test:orchestrator:1.2.0' // Really important to prevent "Empty test suite"

    // --- ARCH ---
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'

    // --- Room ---
    implementation 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'
    implementation 'androidx.room:room-rxjava2:2.2.5'

    // --- Specific lib of the app ---

    // Coil
    implementation 'io.coil-kt:coil:0.9.5'

    // Big image viewer
    implementation 'com.github.piasy:BigImageViewer:1.6.3'

    // ViewPagerLayoutManager
    implementation(name: 'viewpagerlayoutmanager-release', ext: 'aar')

    // ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:2.11.5'

    // Like button (Twitter)
    implementation 'com.github.jd-alexander:LikeButton:0.2.3'
}